plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.0'
    id 'io.gitlab.arturbosch.detekt' version '1.16.0'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
}

repositories {
    mavenCentral()
}

afterEvaluate {
    githubRelease {
        token project.findProperty('github.token') ?: ''
        owner project.findProperty('github.username') ?: ''
        repo 'cutlass'
        prerelease true
        tagName '0.2-SNAPSHOT'
        releaseName '0.2-SNAPSHOT'
        targetCommitish 'main'
        releaseAssets project.files({
            project(":cutlass-cli").tasks.named("minimizedJar").get()
                    .outputs
                    .files
        })
    }

    tasks.githubRelease.dependsOn rootProject.getTasksByName('minimizedJar', true)
}

def ignored_publishing_projects = [
        'cutlass-cli',
        'cutlass-gradle-plugin'
] as HashSet

subprojects {
    group 'io.strlght.cutlass'
    version '0.2.0'

    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'jacoco'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    repositories {
        mavenCentral()
        maven { url 'https://kotlin.bintray.com/kotlinx' }
    }

    dependencies {
        detektPlugins libs.detekt.ktlint

        api libs.kotlin.stdlib

        testImplementation libs.bundles.testDependencies
    }

    compileKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = [
                    "-Xinline-classes",
            ]
        }
    }

    jacocoTestReport {
        dependsOn test
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination layout.buildDirectory.dir('jacocoHtml').get().asFile
        }
    }

    if (!ignored_publishing_projects.contains(project.name)) {
        apply {
            plugin 'maven-publish'
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    groupId = group
                    artifactId = project.name
                    version = version
                    from components.java
                }
            }
        }
    }
}
